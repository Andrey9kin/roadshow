apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'findbugs'
apply plugin: 'maven-publish'
apply plugin: 'jetty'

group = 'net.praqma.roadshow.buep' // FIXME - add your initials or github id here
version = '0.0.34-SNAPSHOT'

// FIXME - should test for blank also
ext.artifactoryUrlAndPort = System.getenv("ARTIFACTORY_URL_AND_PORT") ?: "http://roadshowartifactory:8081/artifactory"
//ext.tomcatUrlAndPort = System.getenv("TOMCAT_URL_AND_PORT") ?: "http://roadshowtomcatserver:8080/"
ext.deployContext = System.getenv("DEPLOY_CONTEXT") ?: "RC" // should be either RC or REL
ext.deployPath = System.getenv("DEPLOY_PATH") ?: String.format("RoadShow-%s", ext.deployContext) // should be set to RoadShow-yourinitial
ext.appName = String.format("/%s/Calculator", ext.deployPath)



description = """"""

sourceCompatibility = 1.6
targetCompatibility = 1.6

def repositoryUrl(name) {
    ext.artifactoryUrlAndPort + "/${name}"
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:'2.26.0'
    compile group: 'org.seleniumhq.selenium', name: 'selenium-server', version:'2.28.0'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'
    compile group: 'junit', name: 'junit', version:'4.7'
}

jacoco {
    toolVersion = "0.7.1.201405082137"
}

test {
    filter {
        includeTestsMatching "net.praqma.roadshow.unit.*"
   }
}

// Modified test task to use on Jenkins so test results
// are seen as new, even though gradle is to smart to run
// the tasks again. The Jenkins JUnit plugin fails the build
// if test results are not new, eg. when building manually.
task jenkinstest {
		description "Same as test task, but renews timestamp on junit xml files. For use on Jenkins"
		dependsOn test
    doLast {
        def timestamp = System.currentTimeMillis()
        fileTree(dir: "${buildDir}/test-results/", include: "TEST*.xml").each {
            it.lastModified = timestamp 
        }
    }
}       

stopPort = 4455
stopKey = 'stop'

task startApp(type: JettyRunWar) {
    daemon = true
}

task functionalTest(type: Test) {
    dependsOn startApp
    filter {
        includeTestsMatching "net.praqma.roadshow.functional.*"
    }
    finalizedBy jettyStop
}

checkstyle {
    ignoreFailures = true
}

findbugs {
    toolVersion = "2.0.1"
    ignoreFailures = true
}

publishing {
    repositories {
        maven {
            url repositoryUrl("snapshot")
        }
    }
    publications {
        roadShow(MavenPublication) {
            from components.java
        }
    }
}

